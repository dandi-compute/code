params.executor = "slurm"

// define your preferred queues here
params.default_queue = 'mit_normal'
params.gpu_queue = 'mit_normal_gpu'
params.max_normal_runtime_in_minutes = 12*60
params.max_gpu_runtime_in_minutes = 6*60

// Helper closures for time calculations
def calcTime = { multiplier, maxMinutes ->
    { def duration = max_duration_minutes.toFloat(); Math.min(duration * multiplier, maxMinutes) + 'm' }
}
def calcMinTime = { divider, minMinutes ->
    { def duration = max_duration_minutes.toFloat(); duration / divider > minMinutes ? duration / divider + 'm' : minMinutes + 'm' }
}

// Detect engine: prefer apptainer if available, else singularity
def hasApptainer = ['bash','-c','command -v apptainer'].execute().waitFor() == 0
def hasSingularity = ['bash','-c','command -v singularity'].execute().waitFor() == 0

if( hasApptainer ) {
    apptainer {
        enabled = true
        autoMounts = true
        apptainer.enabled = true
        apptainer.autoMounts = true
        platform = 'linux/amd64'
        envWhitelist = ['KACHERY_ZONE', 'KACHERY_API_KEY', 'NUMBA_CACHE_DIR', 'HDF5_USE_FILE_LOCKING', 'HF_HOME']
    }
}
else if( hasSingularity ) {
    singularity {
        enabled = true
        autoMounts = true
        singularity.enabled = true
        singularity.autoMounts = true
        platform = 'linux/amd64'
        envWhitelist = ['KACHERY_ZONE', 'KACHERY_API_KEY', 'NUMBA_CACHE_DIR', 'HDF5_USE_FILE_LOCKING', 'HF_HOME']
    }
}
else {
    throw new RuntimeException("Neither apptainer nor singularity found in PATH")
}

process {
    executor = 'slurm'
    queue = params.default_queue  // Default queue assignment
    debug = true

    containerOptions = "--bind \$HOME:\$HOME,/orcd/data/dandi/001:/orcd/data/dandi/001"

    // change max forks for specific processes to allow multiple forks
    withName: job_dispatch {
        cpus=4
        memory='16 GB'
        time='1h'
    }
    withName: preprocessing {
        cpus=32
        memory='256 GB'
        time=calcTime(4, params.max_normal_runtime_in_minutes)
    }
    withName: spikesort_kilosort4 {
        cpus=16
        memory='64 GB'
        containerOptions='--nv'
        clusterOptions='--gres=gpu:1'
        queue=params.gpu_queue ?: params.default_queue
        // Some systems may require 'module cuda' directive
        // module cuda
        time=calcTime(4, params.max_gpu_runtime_in_minutes)
    }
    withName: spikesort_kilosort25 {
        cpus=16
        memory='64 GB'
        containerOptions='--nv'
        clusterOptions='--gres=gpu:1'
        queue=params.gpu_queue ?: params.default_queue
        // Some systems may require 'module cuda' directive
        // module cuda
        time=calcTime(4, params.max_gpu_runtime_in_minutes)
    }
    withName: spikesort_spykingcircus2 {
        cpus=16
        memory='64 GB'
        time=calcTime(4, params.max_normal_runtime_in_minutes)
    }
    withName: postprocessing {
        cpus=16
        memory='64 GB'
        time=calcTime(4, params.max_normal_runtime_in_minutes)
    }
    withName: curation {
        cpus=4
        memory='32 GB'
        // Allocate 10min per recording hour. Minimum 10m
        time=calcMinTime(6, 10)
    }
    withName: visualization {
        cpus=4
        memory='32 GB'
        time=calcTime(2, params.max_normal_runtime_in_minutes)
    }
    withName: results_collector {
        cpus=4
        memory='32 GB'
        time={
            def duration = max_duration_minutes.toFloat() > 10 ? max_duration_minutes.toFloat() : 10
            Math.min(duration, params.max_normal_runtime_in_minutes) + 'm'
        }
    }
    withName: quality_control {
        cpus=16
        memory='64 GB'
        time=calcTime(2, params.max_normal_runtime_in_minutes)
    }
    withName: quality_control_collector {
        cpus=4
        memory='32 GB'
        // Allocate 10min per recording hour. Minimum 10m
        time=calcMinTime(6, 10)
    }
    withName: nwb_subject {
        cpus=4
        memory='32 GB'
        // Allocate 10min per recording hour. Minimum 10m
        time=calcMinTime(6, 10)
    }
    withName: nwb_ecephys {
        cpus=16
        memory='64 GB'
        time=calcTime(2, params.max_normal_runtime_in_minutes)
    }
    withName: nwb_units {
        cpus=4
        memory='32 GB'
        time=calcTime(2, params.max_normal_runtime_in_minutes)
    }
}


dag {
    enabled = true
    file = RESULTS_PATH + '/nextflow/dag.html'
    overwrite = true
}

report {
    enabled = true
    file = RESULTS_PATH + '/nextflow/report.html'
    overwrite = true
}

timeline {
    enabled = true
    file = RESULTS_PATH + '/nextflow/timeline.html'
    overwrite = true
}

trace {
    enabled = true
    file = RESULTS_PATH + '/nextflow/trace.txt'
    overwrite = true
}
